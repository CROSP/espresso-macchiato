plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
    id "co.riiid.gradle" version "0.4.2"
}

apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        // target lowest min sdk which is supported by espresso
        minSdkVersion 8
        targetSdkVersion 23

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // support code coverage for testing
            testCoverageEnabled = true
        }
    }
}

dependencies {
    compile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude module: 'junit' // because of lint error
    }
    compile 'com.android.support:design:23.2.0'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestCompile project(':espressotools')
}

// Original see: http://ryanharter.com/blog/2013/07/30/automatic-versioning-with-git-and-gradle/
def currentReleaseTag = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--match', 'release*'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// plugin configuration to publish release artifacts to bintray
publish {
    groupId = 'de.nenick'
    artifactId = 'espresso-macchiato'
    publishVersion = currentReleaseTag().replace("release-", "")
    desc = 'Oh hi, this is a nice description for a project, right?'
    website = 'https://github.com/nenick/espresso-macchiato'
    autoPublish = false // just a try
}

// plugin configuration to create release tag and push to github
scmVersion {
    tag {
        prefix = 'release'
    }
}

// plugin configuration to publish release artifacts to github
github {
    //noinspection GroovyAssignabilityCheck
    owner = 'nenick'
    repo = 'espresso-macchiato'
    token = 'XXXXXXXXXXXXXXXXXXXXX'
    tagName = currentReleaseTag()
    targetCommitish = 'master'
    name = currentReleaseTag()
    body = new File('../RELEASE_NOTES.md').text
    assets = ['library/build/outputs/aar/library-release.aar']
}